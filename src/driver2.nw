% -*- mode: Noweb; noweb-code-mode: caml-mode -*-
% vim: ts=8 sts=4 sw=4 et
% Grades     ::=    "%%Grades:" Quality Importance Urgency
% Quality    ::=    A|B|C|D|E
% Importance ::=    Central|Subsystem|Peripheral
% Urgency    ::=    Immediate|Soon|Later
%
% Example (at beginning of line): %%Grades: B Central Soon

% ------------------------------------------------------------------ 
\section{Driver}
% ------------------------------------------------------------------ 

This module presents the compiler as a compositional collection of
abstract values and functions. These functions and values are exported
as primitives to a Lua interpreter, giving the user a way to customize
the compiler.
@

In case of an error, all functions raise [[Error.ErrorExn]]. An error
message is typically printed to [[stderr]] before the exception is
raised.

\begin{itemize}
\item [[dummy]]: represents our only target architecture.
\item [[version]]: print a version information to [[stdout]]. This is
      intentionally not a string such it is harder to abuse.
\item [[scan]]: scans a file and prints all tokens together with their
      source code position. Only useful to debug the scanner.
\item [[parse]]: returns the abstract syntax of a {\PAL} file.
\item [[emit_asdl]]: writes the {\AST} in {\small ASDL} syntax to
      [[stdout]].
\item [[check]]: checks the static semantics of a file and returns 
      an environment [[env]] value upon success. 
\item [[compile]]: compiles an {\AST} to assembly code. The function
      calls the Lua function [[optimize]] for every function and passed
      the functions control-flow graph [[cfg]] and its [[stack]] to it.
\item [[assemble]]: emits an assembler program.
\item [[print]]: prints a [[doc]] value to [[stdout]], formatting the
      output for a line width of [[width]] characters.       
\end{itemize}

The compiler is controlled by a Lua interpreter that is build in the
\module{main2} module. It takes several user-defined types to extend it.
This module provides new Lua types like [[asm]] and [[ast]] for the
interpreter, and a library that uses them. 

<<driver2.mli>>=
module Export : sig
    module AsmT     : Lua.USERDATA
    module AstT     : Lua.USERDATA
    module EnvT     : Lua.USERDATA
    module ProcT    : Lua.USERDATA with type 'a t = Proc.t
    module TargetT  : Lua.USERDATA with type 'a t = Target2.t
    module DocT     : Lua.USERDATA

    module MakeLib  
      (AsmV    : Lua.Lib.TYPEVIEW with type 'a t        = 'a AsmT.t)
      (AstV    : Lua.Lib.TYPEVIEW with type 'a t        = 'a AstT.t 
                                  and  type 'a combined = 'a AsmV.combined)
      (EnvV    : Lua.Lib.TYPEVIEW with type 'a t        = 'a EnvT.t 
                                  and  type 'a combined = 'a AsmV.combined)
      (ProcV   : Lua.Lib.TYPEVIEW with type 'a t        = 'a ProcT.t
                                  and  type 'a combined = 'a AsmV.combined)
      (TargetV : Lua.Lib.TYPEVIEW with type 'a t        = 'a TargetT.t 
                                  and  type 'a combined = 'a AsmV.combined)
      (DocV    : Lua.Lib.TYPEVIEW with type 'a t        = 'a DocT.t 
                                  and  type 'a combined = 'a AsmV.combined)
      (InOutV  : Lua.Lib.TYPEVIEW with type 'a t        = 'a Luaiolib.T.t    
                                  and  type 'a combined = 'a AsmV.combined)
        : Lua.Lib.TYPEFUL with type 'a combined = 'a AsmV.combined 
end
@


% ------------------------------------------------------------------ 
\subsection{Caml Implementation}
% ------------------------------------------------------------------ 

@

<<Export>>=
module Personality = struct
    let target = Targets.dummy
end

module E   = Error
module L   = Live.Make(Cfg4)
module Asm = Astasm.Make(Personality)
module IR  = Ast3ir
module F   = Fenv.Clean

<<Caml>>=

let dummy   = Targets.dummy

<<Export>>=
let version () =
    ( This.name     stdout 
    ; output_string stdout " "
    ; This.version  stdout 
    ; output_string stdout 
        "\n(c) 2000, 2001 President and Fellows of Harvard University\n" 
    ; output_string stdout 
        "See also http://www.cminusminus.org/\n"
    )

<<Export>>=
let scan file =
    let fd          = try open_in file
                      with Sys_error(msg) -> E.error msg    in
    let finally ()  = close_in fd                           in
    let lexbuf      = Lexing.from_channel fd                in
    let map         = Srcmap.mk ()                          in
    let scanner     = Scan.scan map                         in
    let location lb = Srcmap.location map (Lexing.lexeme_start lb) in
    let rec loop lb =
        match scanner lb with
            | Parse.EOF -> ()
            | tok       ->
                let (file,line,col) = location lb           in
                let tok             = Scan.tok2str tok      in
                    ( Printf.printf "%-16s %3d %2d %s\n" file line col tok
                    ; loop lb
                    )
    in
        ( Srcmap.sync map 0 (file,1,1)
        ; loop lexbuf
        ; finally ()
        )

<<Export>>=
let parse (file:string) = 
    let fd          = try open_in file
                      with Sys_error(msg) -> E.error msg    in
    let finally ()  = close_in fd                           in
    let lexbuf      = Lexing.from_channel fd                in
    let map         = Srcmap.mk ()                          in
    let scanner     = Scan.scan map                         in
        try
            ( Srcmap.sync map 0 (file,1,1)
            ; (map, Parse.program scanner lexbuf) 
            )
        with
            | Parsing.Parse_error ->
              ( finally()
              ; E.errorPointPrt (map, Lexing.lexeme_start lexbuf) "parse error"
              ; E.error "parse error - compilation aborted"
              )
            | E.ErrorExn msg ->
              ( finally()
              ; E.errorPointPrt (map, Lexing.lexeme_start lexbuf) msg
              ; E.error "parse error - compilation aborted"
              )
            | e ->
              ( finally()
              ; raise e
              )

<<Export>>=
let emit_asdl (map,ast) =
        AstUtil.sexp_wr_program ast stdout
@

Assemblers are objects. We therefore need function to create them, Here
are our current assemblers.

<<Caml old>>=
let sparcasm    (solaris:bool) (c:out_channel)  = Sparcasm.make solaris c
@

The user specifies in the source code some target properties. They must
be consistent with the actual target. We check this in
[[consistency_check]].
<<Export>>=
let consistency_check env (t:Target2.t) =
    let int i = string_of_int i in
    let msg property source target = 
        let m = Printf.sprintf 
                "mismatch of %s between source (%s) and target (%s)"
                property source target 
        in Error.error (m)
    in match F.findEndianness env, t.Target2.byteorder with
    | F.Big,    Rtl.LittleEndian -> msg "byteorder" "little" "big"
    | F.Little, Rtl.BigEndian    -> msg "byteorder" "big"    "little"
    | _ -> if F.findWordsize env <> t.Target2.wordsize then
        msg "wordsize" (int (F.findWordsize env)) (int t.Target2.wordsize)
    else if F.findMemsize env <> t.Target2.memsize then
        msg "memsize" (int (F.findMemsize env)) (int t.Target2.memsize)
    else if F.findPointersize env <> t.Target2.pointersize then
        msg "pointer size" (int (F.findPointersize env)) 
            (int t.Target2.pointersize)
    else if F.findFloat env <> t.Target2.float then
        msg "floating point standard" (F.findFloat env) t.Target2.float
    else if F.findCharset env <> t.Target2.charset then
        msg "charset" (F.findCharset env) t.Target2.charset
    else
        ()
@


<<Export>>=
let check (map,ast) (asm) =
    let env      = Fenv.Dirty.empty map asm          in
    let env      = Fenv.Dirty.push env (Fenv.Dirty.emptyscope) in
    let env      = Elab.Env.global   env ast         in
    let env      = Elab.Check.global env ast         in
    let env      = Elab.Env.setDefaults env          in
    let ()       = if Fenv.Dirty.errorFlag env then 
                      Error.error "compilation aborted because of errors." 
    in
        Fenv.clean env

<<Export>>=
let compile (opt) (map,ast) (target) (env)  =
    let () = consistency_check env target in   
    IR.translate env target opt ast 

let pretty (map,ast) =
    Astpp.program ast

<<Export>>=
let print (doc) (width) =
    Pp.ppToFile stdout width doc
@

% ------------------------------------------------------------------ 
\subsection{Export to Lua}
% ------------------------------------------------------------------ 

<<driver2.ml>>=
module Export = struct
    <<Export>>
end

<<Export>>=
module AsmT = struct
    type 'a t       = Asm3.assembler
    let tname       = "asm"
    let eq _        = fun x y -> x = y
    let to_string _ = fun t -> "<asm>" 
end
module AstT = struct
    type 'a t       = Srcmap.map * Ast.program
    let tname       = "ast"
    let eq _        = fun x y -> x = y
    let to_string _ = fun t -> "<ast>" 
  end
module EnvT = struct
    type 'a t       = Fenv.Clean.env
    let tname       = "env"
    let eq _        = fun x y -> x = y
    let to_string _ = fun t -> "<env>" 
end
module ProcT = struct
    type 'a t       = Proc.t
    let tname       = "proc"
    let eq _        = fun x y -> x = y
    let to_string _ = fun t -> ("<proc "^t.Proc.symbol#text^">") 
end
module TargetT = struct
    type 'a t       = Target2.t
    let tname       = "target"
    let eq _        = fun x y -> x = y
    let to_string _ = fun t -> "<target " ^ t.Target2.name ^ ">" 
end
module DocT = struct
    type 'a t       = Pp.doc
    let tname       = "doc"
    let eq _        = fun x y -> x = y
    let to_string _ = fun t -> "<doc>" 
end

<<MakeLib>>
@

<<MakeLib>>=
module MakeLib
  (AsmV    : Lua.Lib.TYPEVIEW with type 'a t        = 'a AsmT.t)
  (AstV    : Lua.Lib.TYPEVIEW with type 'a t        = 'a AstT.t 
                              and  type 'a combined = 'a AsmV.combined)
  (EnvV    : Lua.Lib.TYPEVIEW with type 'a t        = 'a EnvT.t 
                              and  type 'a combined = 'a AsmV.combined)
  (ProcV   : Lua.Lib.TYPEVIEW with type 'a t        = 'a ProcT.t
                              and  type 'a combined = 'a AsmV.combined)
  (TargetV : Lua.Lib.TYPEVIEW with type 'a t        = 'a TargetT.t 
                              and  type 'a combined = 'a AsmV.combined)
  (DocV    : Lua.Lib.TYPEVIEW with type 'a t        = 'a DocT.t 
                              and  type 'a combined = 'a AsmV.combined)
  (InOutV  : Lua.Lib.TYPEVIEW with type 'a t        = 'a Luaiolib.T.t    
                              and  type 'a combined = 'a AsmV.combined)
    
    : Lua.Lib.TYPEFUL with type 'a combined = 'a AsmV.combined =

struct
  type 'a combined = 'a AsmV.combined
  module M (C : Lua.Lib.CORE with type 'a V.userdata' = 'a combined) =
    struct
      module V = C.V
      let ( **-> ) = V.( **-> )
      <<export goodies to Lua>>
    end (*M*)
end (*MakeLib*)
@ 
Here we finally bind the exported values to names in the Lua
interpreter. The [[compile]] function receives a lua value such that it
can call Lua functions while it is active.
<<export goodies to Lua>>=
let pf      t = (V.pfunc   t).V.embed 
let func    t = (V.func    t).V.embed 
let closure t = (V.closure t).V.embed 

module Map = struct
    let ast       = AstV.makemap    V.userdata V.projection
    let asm       = AsmV.makemap    V.userdata V.projection
    let env       = EnvV.makemap    V.userdata V.projection
    let proc      = ProcV.makemap   V.userdata V.projection
    let doc       = DocV.makemap    V.userdata V.projection
    let target    = TargetV.makemap V.userdata V.projection
    let optimize  = V.func    (proc **-> V.runit)
    let optimize' = V.closure (proc **-> V.runit)
    let channel   = InOutV.makemap  V.userdata V.projection
    let inchan    = Luaiolib.in'    channel    V.projection
    let outchan   = Luaiolib.out    channel    V.projection
    let stage     = V.pfunc (V.value **-> proc **-> V.result V.bool)
end

let init g = 
  C.register_module "Asm"
    [ "cmm"  , pf (            Map.outchan **-> V.result Map.asm) Asm.asm     
    ; "ast"  , pf (            Map.outchan **-> V.result Map.asm) Asm.asm (*alias*)
    ; "dot"  , pf (            Map.outchan **-> V.result Map.asm) Dotasm.asm
    ; "asdl" , pf (            Map.outchan **-> V.result Map.asm) Asdlasm.asm
    ; "sparc", pf (V.bool **-> Map.outchan **-> V.result Map.asm) Msparcasm.asm
    ] g;
  C.register_module "Driver" 
    [ "assemble"      , pf (Map.asm     **-> V.runit)            (fun asm -> asm#emit)
    ; "proc_assembler", pf (Map.asm     **-> V.result Map.stage) 
                          (fun asm v p -> asm#cfg_instr p.Proc.cfg p.Proc.symbol; true)
    ; "check"         , pf (Map.ast     **-> Map.asm 
                                        **-> V.result Map.env)   check
    ; "emit_asdl"     , pf (Map.ast     **-> V.runit)            emit_asdl
    ; "parse"         , pf (V.string    **-> V.result Map.ast)   parse
    ; "print"         , pf (Map.doc     **-> V.int **-> V.runit) print
    ; "pretty"        , pf (Map.ast     **-> V.result Map.doc)   pretty
    ; "scan"          , pf (V.string    **-> V.runit)            scan
    ; "version"       , pf (V.unit      **-> V.runit)            version
    ; "stdin"         , Map.inchan.V.embed                       stdin (* hack *)
    ; "stdout"        , Map.outchan.V.embed                      stdout(* hack *)
    ; "freeze"        , Map.stage.V.embed (fun v p -> Ast3ir.freeze p; true)
    ; "compile"       , func (Map.optimize  **-> Map.ast  
                                            **-> Map.target 
                                            **-> Map.env 
                                            **-> V.result Map.asm)
                        (fun g opt -> compile (opt g))
    ; "compilec"      , func (Map.optimize' **-> Map.ast  
                                            **-> Map.target 
                                            **-> Map.env 
                                            **-> V.result Map.asm)
                        (fun g opt -> compile (opt g))
    ] g;
  Lua.Lib.StringList.empty
@  
