% -*- mode: Noweb; noweb-code-mode: caml-mode -*-
% vim: ts=8 sts=4 sw=4 et
% Grades     ::=    "%%Grades:" Quality Importance Urgency
% Quality    ::=    A|B|C|D|E
% Importance ::=    Central|Subsystem|Peripheral
% Urgency    ::=    Immediate|Soon|Later
%
% Example (at beginning of line): %%Grades: B Central Soon

% ------------------------------------------------------------------ 
\section{Driver}
% ------------------------------------------------------------------ 

This module presents the compiler as a compositional collection of
abstract values and functions. These functions and values are exported
as primitives to a Lua interpreter, giving the user a way to customize
the compiler.

In case of an error, all functions raise [[Error.ErrorExn]]. An error
message is typically printed to [[stderr]] before the exception is
raised.

\begin{itemize}
\item [[version]]: print a version information to [[stdout]]. This is
      intentionally not a string such it is harder to abuse.
\item [[scan]]: scans a file and prints all tokens together with their
      source code position. Only useful to debug the scanner.
\item [[parse]]: returns the abstract syntax of a {\PAL} file.
\item [[emit_asdl]]: writes the {\AST} in {\small ASDL} syntax to
      [[stdout]].
\item [[check]]: checks the static semantics of a file and returns 
      an environment [[env]] value upon success. 
\item [[compile]]: compiles an {\AST} to assembly code. The function
      calls the Lua function [[optimize]] for every function and passed
      the functions control-flow graph [[cfg]] and its [[stack]] to it.
\item [[assemble]]: emits an assembler program.
\item [[print]]: prints a [[doc]] value to [[stdout]], formatting the
      output for a line width of [[width]] characters.       
\item [[reinit]]: re-initializes any mutable state.
\end{itemize}

The compiler is controlled by a Lua interpreter that is build in the
\module{main2} module. It takes several user-defined types to extend it.
This module provides new Lua types like [[asm]] and [[ast]] for the
interpreter, and a library that uses them. 
<<driver.mli>>=
val check     : 
  Srcmap.map * Ast.program -> 'proc Asm.assembler -> 'proc Ofenv.Clean.env'
val emit_asdl : 'a * Ast.program -> unit
val parse     : string -> Srcmap.map * Ast.program
val print     : Pp.doc -> int -> out_channel -> unit
val pretty    : 'a * Ast.toplevel list -> Pp.doc
val scan      : string -> unit
val version   : unit -> unit
val compile : (Oast2ir.proc -> unit) -> bool -> 'a * Ast.program -> Oast2ir.tgt
                -> Oast2ir.proc Ofenv.Clean.env' -> Oast2ir.proc Asm.assembler
val reinit    : unit -> unit
@

% ------------------------------------------------------------------ 
\subsection{Caml Implementation}
% ------------------------------------------------------------------ 
<<driver.ml>>=
module E   = Error
module F   = Ofenv.Clean
<<driver.ml>>=
let version () =
    ( This.name     stdout 
    ; output_string stdout " "
    ; This.version  stdout 
    ; output_string stdout 
        " (see also http://www.cminusminus.org)\n"
    )
<<driver.ml>>=
let scan file =
    let fd          = try open_in file
                      with Sys_error(msg) -> E.error msg    in
    let finally ()  = close_in fd                           in
    let lexbuf      = Lexing.from_channel fd                in
    let map         = Srcmap.mk ()                          in
    let scanner     = Scan.scan map                         in
    let location lb = Srcmap.location map (Lexing.lexeme_start lb) in
    let rec loop lb =
        match scanner lb with
            | Parse.EOF -> ()
            | tok       ->
                let (file,line,col) = location lb           in
                let tok             = Scan.tok2str tok      in
                    ( Printf.printf "%-16s %3d %2d %s\n" file line col tok
                    ; loop lb
                    )
    in
        ( Srcmap.sync map 0 (file,1,1)
        ; loop lexbuf
        ; finally ()
        )
<<driver.ml>>=
let parse (file:string) = 
    let fd          = try open_in file
                      with Sys_error(msg) -> E.error msg    in
    let finally ()  = close_in fd                           in
    let lexbuf      = Lexing.from_channel fd                in
    let map         = Srcmap.mk ()                          in
    let scanner     = Scan.scan map                         in
        try
            ( Srcmap.sync map 0 (file,1,1)
            ; (map, Parse.program scanner lexbuf) 
            )
        with
            | Parsing.Parse_error ->
              ( finally()
              ; E.errorPointPrt (map, Lexing.lexeme_start lexbuf) "parse error"
              ; E.error "parse error - compilation aborted"
              )
            | E.ErrorExn msg ->
              ( finally()
              ; E.errorPointPrt (map, Lexing.lexeme_start lexbuf) msg
              ; E.error "parse error - compilation aborted"
              )
            | e ->
              ( finally()
              ; raise e
              )
<<driver.ml>>=
let emit_asdl (map,ast) =
        AstUtil.sexp_wr_program ast stdout
@
The user specifies in the source code some target properties. They must
be consistent with the actual target. We check this in
[[consistency_check]].
THIS CHECK SHOULD PROBABLY BE MOVED INTO ELAB, WHICH SHOULD ALSO CHECK FOR
THINGS LIKE CORRECT CALLING CONVENTIONS AND HARDWARE REGISTERS.
<<driver.ml>>=
let consistency_check env (t:('a, 'b) Target.t) =
    let int i = string_of_int i in
    let msg property source target = 
        let m = Printf.sprintf 
                "source code specifies %s %s, but target %s specifies %s %s"
                property source t.Target.name property target 
        in Error.error (m)
    in match F.findEndianness env, t.Target.byteorder with
    | F.Big,    Rtl.LittleEndian -> msg "byteorder" "big"    "little"
    | F.Little, Rtl.BigEndian    -> msg "byteorder" "little" "big"
    | _ -> if F.findWordsize env <> t.Target.wordsize then
        msg "wordsize" (int (F.findWordsize env)) (int t.Target.wordsize)
    else if F.findMemsize env <> t.Target.memsize then
        msg "memsize" (int (F.findMemsize env)) (int t.Target.memsize)
    else if F.findPointersize env <> t.Target.pointersize then
        msg "pointer size" (int (F.findPointersize env)) 
            (int t.Target.pointersize)
    else if F.findFloat env <> t.Target.float then
        msg "floating-point standard" (F.findFloat env) t.Target.float
    else if F.findCharset env <> t.Target.charset then
        msg "charset" (F.findCharset env) t.Target.charset
    else
        ()
@
<<driver.ml>>=
let check (map,ast) (asm) =
    let env      = Ofenv.Dirty.empty map asm          in
    let env      = Ofenv.Dirty.push env (Ofenv.Dirty.emptyscope) in
    let env      = Oelab.Env.global   env ast         in
    let env      = Oelab.Check.global env ast         in
    let env      = Oelab.Env.setDefaults env          in
    let ()       = if Ofenv.Dirty.errorFlag env then 
                      Error.error "compilation aborted because of errors." in
    Ofenv.clean env

let compile opt exportglobals (map,ast) target env  =
    let () = consistency_check env target in   
    Oast2ir.translate target env opt exportglobals ast 
<<driver.ml>>=
let pretty (map,ast) = Astpp.program ast
let print doc width channel = Pp.ppToFile channel width doc
@
If multiple files are ocmpiled in a single pass, we do not want to
output to depend on the sequence in which the files are processed.
Calling [[reinit]] will reset any mutable state that can effect the
output. Currently only [[Idgen]] is known to have this problem.
<<driver.ml>>=
let reinit() = Idgen.reset()
@
